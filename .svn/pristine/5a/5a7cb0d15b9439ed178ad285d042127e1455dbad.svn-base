#version 330 core
#extension GL_EXT_gpu_shader4 : enable
layout (points) in;
layout (triangle_strip,max_vertices=24) out;

struct IOStruct {
 vec4 textureAtlasOffset;
 vec3 worldPosition;
 float glass;
 float water;
 float lava;
 float stained_glass;
 vec4 vertex;
 vec4 color;
};
in IOStruct vsOutput[];
out IOStruct gsOutput;
uniform mat4x4 worldMatrix;
uniform mat4x4 viewProj;

#define CVEC3(x,y,z) vec4(x, y, z,0)
int faces[6] = int[]( 32, 16, 8, 4, 2, 1 ); 
int cubeFaceIndices[24] = int[]( 
	1,0,5,4, //front  
	6,2,7,3, // back
	3,1,7,5, // top 
	2,6,0,4,  //bottom
	5,4,7,6,  //right
	0,1,2,3  // left
); 
vec4 cubeVerts[8] = vec4[](
	CVEC3(-0.5, -0.5, 0.5), CVEC3(-0.5,0.5,0.5), CVEC3(-0.5,-0.5,-0.5), CVEC3(-0.5,0.5,-0.5),
	CVEC3(0.5, -0.5, 0.5), CVEC3(0.5,0.5,0.5), CVEC3(0.5,-0.5,-0.5), CVEC3(0.5,0.5,-0.5)
);

void main()
{       
	int zed = int(ceil(vsOutput[0].color.z*63));// color.zed encodes which faces to render in base 10.
	//zed=63;
	//check faces from highest number to lowest

	for(int ctr = 0; ctr < 1; ctr++) {
		if(gl_in[ctr].gl_Position.x == 2.0) continue;
		for(int i = 0; i < 6; i++) {
			if(zed >= faces[i]) {
				zed -= faces[i];
				for(int lp = 0; lp < 4; lp++) {
					vec4 tpos = vsOutput[ctr].vertex + cubeVerts[cubeFaceIndices[(i*4)+lp]];
					gl_Position = viewProj*tpos;
					gsOutput = vsOutput[ctr];
					gsOutput.worldPosition = tpos.xyz+vec3(0.5,0.5,0.5);	
					EmitVertex();
				}
				EndPrimitive(); 
				
			}
		}
    
	}
}

		
		/*****************************

			Cube vertice order             
						definition                       +Y
                                                          |
						3_______7_                        | +Z
						/|       /| |                     | /
                       / |      / | | +                   |/
                      /  |     /  | | Y          -X-------/--------+X
                    1/___|___5/_ 6| | -                  /| 
                    |   /2   |   / /                  -Z  |
                    |  /        / / +                     |
                    | /      | / / Z                     -Y
                    |/_______|/ / -
                    0--------4-/ 
                      - X +

		Winding: Clockwise: (TWO TRI OR QUAD)
		Left: 0-1-3,0-3-2 OR 1,3,0,2
		Right: 4-5-7,4-7-6 OR 5,7,4,6
		Front: 0-1-5,0,5,4 OR 1,5,0,4
		back:6-7-3,6-3-2 OR 7,3,6,2
		top: 1-3-7,1-7-5 OR 3,7,1,5
		bottom: 6-4-0,6-0-2 OR 2,0,6,4
		******************************/